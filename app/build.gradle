import groovy.json.JsonSlurper

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    classpath 'com.testdroid:gradle:1.5.0'
    classpath 'org.apache.httpcomponents:httpclient-android:4.3.3'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'checkstyle'
apply plugin: 'testdroid'
apply plugin: 'spoon'

repositories {
  mavenCentral()
  jcenter()
  maven {
    url 'https://maven.google.com'
  }
}

String[] archs = ['arm64-v8a', 'armeabi', 'mips', 'mips64', 'x86', 'x86_64']

dependencies {

  // Get kiwixlib online if it is not populated locally
  if (file("../kiwixlib/src/main").list().length == 1) {
    compile 'org.kiwix.kiwixlib:kiwixlib:0.0.6'
  } else {
    compile project(':kiwixlib')
    archs = file("../kiwixlib/src/main/jniLibs").list()
  }

  // Storage Devices
  compile 'eu.mhutti1.utils.storage:android-storage-devices:0.6.2'

  // Android Support
  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.android.support:support-v13:25.3.1'
  compile 'com.android.support:support-v4:25.3.1'
  compile 'com.android.support:design:25.3.1'
  compile 'com.android.support:cardview-v7:25.3.1'

  compile 'com.android.support:multidex:1.0.1'
  compile 'com.android.support.test:runner:1.0.0'

  androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

  androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0', {
    exclude group: 'com.android.support', module: 'support-annotations'
  }

  androidTestCompile 'com.android.support.test.espresso:espresso-web:3.0.0'
  androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:3.0.0'
  androidTestCompile 'com.android.support.test.espresso:espresso-contrib:3.0.0', {
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'appcompat-v7'
    exclude group: 'com.android.support', module: 'design'
    exclude group: 'com.android.support', module: 'recyclerview-v7'
  }

  androidTestCompile 'com.android.support:support-annotations:25.3.1'
  androidTestCompile 'com.android.support.test:runner:1.0.0'
  androidTestCompile 'com.android.support.test:rules:1.0.0'

  // Guava
  compile group: 'com.google.guava', name: 'guava', version: '20.0'

  // Dagger
  compile 'com.google.dagger:dagger:2.0.2'
  apt "com.google.dagger:dagger-compiler:2.0.2"
  androidTestApt "com.google.dagger:dagger-compiler:2.0.2"

  // SquiDB
  compile 'com.yahoo.squidb:squidb:2.0.0'
  compile 'com.yahoo.squidb:squidb-annotations:2.0.0'
  apt 'com.yahoo.squidb:squidb-processor:2.0.0'

  // Apache
  compile 'commons-io:commons-io:2.5'

  // Square
  compile 'com.squareup.okhttp3:okhttp:3.6.0'
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  compile('com.squareup.retrofit2:converter-simplexml:2.1.0') {
    exclude group: 'xpp3', module: 'xpp3'
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
  }
  androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
  androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'

  // Butterknife
  compile 'com.jakewharton:butterknife:8.0.1'
  apt 'com.jakewharton:butterknife-compiler:8.0.1'

  // RxJava
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'io.reactivex:rxjava:1.1.3'

  // JUnit
  testCompile 'junit:junit:4.12'
  androidTestCompile 'junit:junit:4.12'

  // Mockito
  testCompile "org.mockito:mockito-core:2.7.22"
  androidTestCompile "org.mockito:mockito-android:2.7.22"
}

// Set custom app import directory
def map = [:]
def custom = new File("app/src")
if (project.hasProperty('customDir')) {
  custom = file(project.property('customDir'))
}

// Set up flavours for each custom app in the directory
if(custom.listFiles()) {
  custom.eachFile() { file ->

    def fileName = file.getName()
    if (fileName.startsWith(".") || fileName.contains("test") || fileName == "main" || fileName.contains("Test")) {
      return
    }
    map.put(fileName, file.getAbsolutePath())
  }
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 25
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    // See https://github.com/linkedin/dexmaker/issues/65 for why we need the following line.
    testInstrumentationRunnerArguments.notClass = 'com.android.dex.DexIndexOverflowException'
    multiDexEnabled true
  }

  lintOptions {
    // Treat lint seriously
    abortOnError true
    // Hardcoded strings in xml
    error 'HardcodedText'
    // Hardcoded strings in setText
    error 'SetTextI18n'
    // Strings.xml issues
    warning 'ExtraTranslation'
    warning 'MissingTranslation'

    // Warnings
    warning 'InvalidPackage'
    warning 'StringFormatInvalid'
  }

  buildTypes {

    // Main build type for debugging
    debug {
      buildConfigField "String", "KIWIX_DOWNLOAD_URL", "\"http://download.kiwix.org/\""
      // True breaks local variables being shown in breakpoints
      testCoverageEnabled false
      // Needed for instrumentation tests on Pre 5.0
      multiDexKeepProguard file('multidex-instrumentation-config.pro')
    }

    mock_network {
      initWith(buildTypes.debug)
      // TODO add DI for the mock network
    }

    local_download_server {
      initWith(buildTypes.debug)
      buildConfigField "String", "KIWIX_DOWNLOAD_URL", "\"http://192.168.1.196/\""
    }

    // Used to assess code coverage
    coverage {
      initWith debug
      testCoverageEnabled true
    }

    // Release Type
    release {
      buildConfigField "String", "KIWIX_DOWNLOAD_URL", "\"http://download.kiwix.org/\""
    }

  }

  productFlavors {
    // Vanilla Kiwix app
    kiwix {
      println "Configuring Kiwix"
      // Set vanilla config
      buildConfigField "boolean", "IS_CUSTOM_APP", "false"
      buildConfigField "boolean", "HAS_EMBEDDED_ZIM", "false"
      buildConfigField "String", "ZIM_FILE_NAME", "\"\""
      buildConfigField "long", "ZIM_FILE_SIZE", "0"
      buildConfigField "int", "CONTENT_VERSION_CODE", "0"
      buildConfigField "String", "ENFORCED_LANG", "\"\""
      resValue "string", "app_name", "Kiwix"
      resValue "string", "app_search_string", "Search Kiwix"
      if (project.hasProperty('version_code')) {
        def version_code = project.property('version_code')
        versionCode version_code.toInteger()
      } else {
        versionCode 55
      }
      if (project.hasProperty('version_name')) {
        versionName project.property('version_name')
      } else {
        versionName "2.3"
      }
    }
    // Custom apps built from a json file, zim file and icon set
    map.each { name, directory ->
      "$name" {
        println "Configuring $name"
        if (file(directory + "/build.gradle").exists()) {
          apply from: directory + "/build.gradle"
        }
        def jsonFile = ""
        if (project.hasProperty('jsonFile')) {
          // Read json file from properties e.g command line
          jsonFile = file(project.property('jsonFile'))
        } else {
          // If no file provided use the test file
          jsonFile = file(directory + '/info.json')
        }
        def parsedJson = new JsonSlurper().parseText(jsonFile.text)
        def sourceFile = file(directory + "/" + parsedJson.zim_file)
        if (parsedJson.embed_zim) {
         // Place content in each lib directory for embeded zims
          for (String archName : archs) {
           copy {
             from sourceFile
             into file(directory + "/jniLibs/" + archName)
             rename { String filename -> "libcontent.so" }
           }
         }
         parsedJson.zim_file = "libcontent.so"
        }
        // Set custom config from json
        applicationId "$parsedJson.package"
        buildConfigField "boolean", "IS_CUSTOM_APP", "true"
        buildConfigField "boolean", "HAS_EMBEDDED_ZIM", "$parsedJson.embed_zim"
        def filename = ""
        if (parsedJson.zim_file.lastIndexOf('/') >= 0) {
          filename = parsedJson.zim_file.substring(parsedJson.zim_file.lastIndexOf('/') + 1)
        } else {
          filename = parsedJson.zim_file
        }
        buildConfigField "String", "ZIM_FILE_NAME", "\"$filename\""
        if (project.hasProperty('zim_file_size')) {
          def length = Long.parseLong(project.property('zim_file_size'))
          buildConfigField "long", "ZIM_FILE_SIZE", "${length}L"
        } else {
          long length = sourceFile.length()
          buildConfigField "long", "ZIM_FILE_SIZE", "${length}L"
        }
        if (project.hasProperty('version_code')) {
          def version_code = project.property('version_code')
          versionCode version_code.toInteger()
        } else {
          versionCode parsedJson.version_code.toInteger()
        }
        if (project.hasProperty('version_name')) {
          versionName project.property('version_name')
        } else {
          versionName parsedJson.version_name
        }
        if (project.hasProperty('content_version_code')) {
          def content_version_code = project.property('content_version_code')
          buildConfigField "int", "CONTENT_VERSION_CODE", "$content_version_code"
        } else if (parsedJson.content_version_code != null) {
          buildConfigField "int", "CONTENT_VERSION_CODE", "$parsedJson.content_version_code"
        } else if (project.hasProperty('version_code'))  {
          def version_code = project.property('version_code')
          buildConfigField "int", "CONTENT_VERSION_CODE", "$version_code"
        } else if (parsedJson.version_code != null) {
          buildConfigField "int", "CONTENT_VERSION_CODE", "$parsedJson.version_code"
        }
        buildConfigField "String", "ENFORCED_LANG", "\"$parsedJson.enforced_lang\""
        resValue "string", "app_name", "$parsedJson.app_name"
        resValue "string", "app_search_string", 'Search ' + "$parsedJson.app_name"
      }
    }
  }

  // Set each custom apps respective source directory
  sourceSets {
    map.each { name, directory ->
      "$name" {
        setRoot directory
        jni.srcDirs = []
        jniLibs.srcDir directory + '/jniLibs'
      }
    }
  }

  compileOptions {
    encoding = "UTF-8"
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions {
    javaMaxHeapSize "4g"
  }

  /*
  Add back once proguard is configured
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt')
    }
  }
  */
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}

// Testdroid deployment configuration
def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER')

def findJar(prefix) {
  configurations.runtime.filter { it.name.startsWith(prefix) }
}

testdroid {
  authorization "APIKEY"
  apiKey System.getenv('BITBAR_API_KEY')
  deviceGroup 'Kiwix'
  projectName "Kiwix"
  testRunName "Auto Test " + buildNumber

  fullRunConfig {
    instrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
  }
}
