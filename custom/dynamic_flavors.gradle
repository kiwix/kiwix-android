import custom.CustomApps
import groovy.json.JsonSlurper

apply plugin: 'com.android.application'
String[] archs = ['arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64']

// Set custom app import directory
def map = [:]
def custom = new File("custom/src")
if (project.hasProperty("customDir")) {
  custom = file(project.property("customDir"))
}
// Set up flavours for each custom app in the directory
if (custom.listFiles()) {
  custom.eachFile() { file ->

    def fileName = file.getName()
    if (fileName.startsWith(".") ||
      fileName.contains("test") ||
      fileName == "main" ||
      fileName.contains("Test")) {
      return
    }
    map.put(fileName, file.getAbsolutePath())
  }
}
android {
  productFlavors {
    // Custom apps built from a json file
    map.each { name, directory ->
      def jsonFile = file(directory + "/info.json")
      if (jsonFile.exists()) {
        def parsedJson = new JsonSlurper().parseText(jsonFile.text)
        CustomApps.INSTANCE.createCustomAppFromJson(
          name,
          parsedJson.zim_url,
          parsedJson.enforced_lang,
          parsedJson.name,
          parsedJson.version_name
        ).createFlavor(it)
      }
    }
  }
}
